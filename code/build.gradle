plugins {
    // we can not use ext variables here
    id "java"
    id "checkstyle"
    id "com.github.spotbugs" version "5.+"
    id "com.diffplug.spotless" version "6.5.+"
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    gradlePluginPortal()
    google()
}

ext {
    // we can not use the + operator here
    gdxVersion = "1.10.1-SNAPSHOT"
    aiVersion = "1.8.2"
    gsonVersion = "2.7"
}

dependencies {
    implementation "io.github.pm-dungeon:core:4.+"

    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"

    // JUnit 4, Mockito and Powermock for testing:
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.+"

    // https://mvnrepository.com/artifact/org.powermock/powermock-module-junit4
    testImplementation "org.powermock:powermock-module-junit4:2.+"
    // https://mvnrepository.com/artifact/org.powermock/powermock-api-mockito2
    testImplementation "org.powermock:powermock-api-mockito2:2.+"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: "com.google.code.gson", name: "gson", version: gsonVersion
}

sourceCompatibility = 17
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["desktop/src/"]
sourceSets.main.resources.srcDirs = ["assets/"]
sourceSets.test.java.srcDirs = ["desktop/test/"]

project.ext.mainClassName = "desktop.MyGame"
project.ext.assetsDir = new File("assets")

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        allJvmArgs += ["-XstartOnFirstThread"]
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    debug = true
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) {
        allJvmArgs += ["-XstartOnFirstThread"]
    }
    doFirst {
        println(DefaultNativePlatform.currentOperatingSystem)
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    configFile file("checkstyle-config/checks.xml")
    toolVersion "9.2.1"
    showViolations = true
}

spotbugs {
    toolVersion = "4.2.2"
    ignoreFailures = true
    showStackTraces = false
}

spotbugsMain {
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugsXml.xml")
        }
    }
}

spotless {
    java {
        googleJavaFormat().aosp()
    }
}
